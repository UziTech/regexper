grammar JavascriptRegexp
  root <- ( ( "/" regexp "/" flags:[gimsxADSUXJu]* ) / regexp flags:""? ) <Root>
  inline_flag <- "(?" flags:[imsxUXJ]+ ")" <InlineFlag>
  regexp <- match:match alternates:( "|" match )* <Regexp>
  match <- (!repeat) parts:match_fragment* <Match>
  anchor <- ( "^" / "$" / "\\" [AGZz] ) <Anchor>
  match_fragment <- content:( anchor / inline_flag / subexp / subroutine  / comment / charset / terminal ) repeat:repeat? <MatchFragment>
  repeat <- spec:( repeat_any / repeat_required / repeat_optional / repeat_spec ) modifier:[?+]? <Repeat>
  repeat_any <- "*" <RepeatAny>
  repeat_required <- "+" <RepeatRequired>
  repeat_optional <- "?" <RepeatOptional>
  repeat_spec <- ( "{" min:numeric+ "," max:numeric+ "}"
                 / "{" min:numeric+ ",}"
                 / "{" exact:numeric+ "}" ) <RepeatSpec>
  subexp <- "(" capture:( "?<" groupname:name ">" / "?:" / "?=" / "?!" / "?<=" / "?<!" / "?>" )? regexp ")" <Subexp>
  subroutine <- "(?" group:( "R" / numeric+ / "&" name ) ")" <Subroutine>
  comment <- "(?#" comment:[^)]* ")" <Comment>
  charset <- "[" invert:"^"? parts:( posix / charset_range / charset_terminal )* "]" <Charset>
  posix <- "[:" invert:"^"? name:posix_name ":]" <Posix>
  posix_name <- ( "<" / ">" / "alnum" / "alpha" / "ascii" / "blank" / "cntrl" / "digit" / "graph" / "lower" / "print" / "punct" / "space" / "upper" / "word" / "xdigit" )
  charset_range <- first:charset_range_terminal "-" last:charset_range_terminal <CharsetRange>
  charset_terminal <- charset_escape <CharsetEscape>
                    / charset_literal <Literal>
  charset_range_terminal <- charset_range_escape <CharsetEscape>
                          / charset_literal <Literal>
  charset_escape <- "\\" esc:(
         code:[abdDefhHnNrRsStvVwW] arg:""?
       / control_escape
       / octal_escape
       / hex_escape
       / unicode_escape
       / null_escape )
  charset_range_escape <- "\\" esc:(
         code:[bfhHnNrRtvV] arg:""?
       / control_escape
       / octal_escape
       / hex_escape
       / unicode_escape
       / null_escape )
  charset_literal <- ( ""? literal:[^\\\]] )
                   / ( literal:"\\" &"c" )
                   / ( "\\" literal:[^abdDefhHnNrRsStvVwW] )
  terminal <- "." <AnyCharacter>
            / escape <Escape>
            / literal <Literal>
  escape <- "\\" esc:(
         code:[abBdDefhHnNrRsStvVwW1-9] arg:""?
       / control_escape
       / octal_escape
       / hex_escape
       / unicode_escape
       / null_escape )
  literal <- ( ""? literal:[^|\\/.\[\(\)?+*$^] )
           / ( literal:"\\" &"c" )
           / ( "\\" literal:. )

  name <- alpha (alpha / numeric)*
  alpha <- [a-zA-Z]
  numeric <- [0-9]
  hex <- [0-9a-fA-F]

  control_escape <- code:"c" arg:alpha
  octal_escape <- code:"0" arg:[0-7]+
  hex_escape <- code:"x" arg:( hex hex )
  unicode_escape <- code:"u" arg:( hex hex hex hex )
  null_escape <- code:"0" arg:""?
